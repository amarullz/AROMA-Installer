### LICENSE:
#
# Copyright (C) 2011 Ahmad Amarullah ( http://amarullz.com/ )
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#      http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.
#
### FILE INFO:
#
# AROMA Installer 1.50 - AROMA Installer Test Script Configuration
#       (c) 2011-2012 by Ahmad Amarullah
#           amarullz - xda-developers
#           http://www.amarullz.com/
#
#       Binary Version  : 1.50
#       Binary Codename : Cempaka
#
### INFO:
#
# * AROMA Resource Dir  = META-INF/com/google/android/aroma
#
# * AROMA Temporary Dir = /tmp/aroma-data/
#
# * About Icon          = @alert - show <AROMA Resource Dir>/icons/alert.png
#                         or if theme used it will find <themedir>/icon.alert.png
#                         you can still access it via "icons/alert"
#
# * dp                  = Device Pixelate. WVGA/QHD = 3, HVGA = 2.
#                         dp calculated with = round(min(width,height) / 160)
#
###


##
# Calibrate Touchscreen
#  defaut: HTC Desire Touch Screen Calibrate Data [7.90,20,7.90,20]
#
# For other device, run the installer, then press "menu"
# and select Calibrating Tools. Follow Instructions, and make
# sure the touch screen has been valid in the testing step.
#
# In the end, there will be alert dialog with calibration data
# ( green ), replace this calibrate() function with that data.
#
#-------- Uncomment This to insert calibrating data:
# calibrate("7.90","20","7.90","20");


##
# Custom Key Code - Usefull For Some Device which not used
#                   Standard key code for up/down/select/back/menu
#
# You can capture the keycode in
# recovery using "aroma-keycapture-flashable.zip" Available in
# AROMA Installer Thread.
#
# Press some key when it run, it will return key code,
# that key can override up/down/select/back/menu actions in
# AROMA Installer, you can even set your home key for up, or
# volume down for select item
#
#-------- Uncomment & Modify this Code:
#
# ini_set("customkeycode_up",     "115");
# ini_set("customkeycode_down",   "114");
# ini_set("customkeycode_select", "116");
# ini_set("customkeycode_menu",   "229");
# ini_set("customkeycode_back",   "158");
#

##
# Initializing Rom Information
#
ini_set("rom_name",             "AROMA Test Zip");
ini_set("rom_version",          "1.50");
ini_set("rom_author",           "amarullz");
ini_set("rom_device",           "Any Device");


##
# Show Simple Splash
#
splash(
  #-- Duration 2000ms / 2 seconds
    2000,
  
  #-- <AROMA Resource Dir>/sample.png
    "sample"
);

##
# Show Animated Splash
#
anisplash(
  #-- Number of Loop
    8,
  
  #-- Frame 1 [ Image, duration in millisecond ]
  #-- <AROMA Resource Dir>/splash/a1.png
    "splash/a1", 500,
    "splash/a2", 30,
    "splash/a3", 30,
    "splash/a4", 30,
    "splash/a5", 30,
    "splash/a6", 30
);

##
# Show Calibrating Tools Directly before show any UI
#
#--- Remove this calibtool if you already know the calibrate data
calibtool("");


##
# STEP 1 - SELECT THEME
#
selectbox(
  #-- Title
    "Select Theme",
  
  #-- Sub Title
    "Please select installer theme that you want to use in this installation test:",
  
  #-- Icon: <AROMA Resource Dir>/icons/personalize.png or <ThemeDir>/icon.personalize.png
    "@personalize",
  
  #-- Will be saved in /tmp/aroma-data/theme.prop
    "theme.prop",
  
  #---------------------------------[ Selectbox Without Group ]----------------------------------#
  # TITLE            |  SUBTITLE                                                 | Initial Value #
  #------------------+-----------------------------------------------------------+---------------#
    "Generic",          "Unthemed AROMA Installer",                                 1,
    "MIUI Theme",       "MIUI Theme by mickey-r & amarullz",                        0,
    "ICS Theme",        "Ice Cream Sandwitch by DemonWav & amarullz",               0,
    "Sense Theme",      "HTC Sense Theme by amarullz",                              0
  #-- Initial Value = 0: Unselected, 1: Selected, 3: Not Visible
);

##
# SET THEME
#
if
  prop("theme.prop", "selected.0") == "2"
then
  theme("miui");
endif;

if
  prop("theme.prop", "selected.0") == "2"
then
  theme("ics");
endif;

if
  prop("theme.prop", "selected.0") == "3"
then
  theme("sense");
endif;

##
# STEP 2 - SHOW INFORMATION
#
viewbox(
  #-- Title
    "Welcome",
  
  #-- Text
    "You are about to Installing AROMA Test Zip for Any Device.\n\n"+
    
    "AROMA Installer was the first and the only Touch and Customizable "+
    "ANDROID ROM Installer in the World.\n\n"+
      "\t\tVERSION : <b>1.50</b> CODENAME: Cempaka\n"+
      "\t\tUPDATED : <b>Feb, 20 2012</b>\n\n\n"+

    "Press Next to Continue the Installation...",
  
  #-- Icon
    "@info"
);


##
# STEP 3 - LICENSE
#
agreebox(
  #-- Title
    "Terms and Conditions",
  
  #-- Subtitle / Description
    "Please read carefully The AROMA Installer Terms and Conditions Below.",
  
  #-- Icon
    "@agreement",
  
  #-- Text Content ( Read from <AROMA Resource Dir>/license.txt )
    resread("license.txt"),
  
  # 5
    "I Agree with this Terms and Conditions...",
  
  # 6
    "Please check the agreement..."
);

##
# Text Box UI. - Same with Agreement, but without checkbox and alert.
#
# Arguments:
#   1. Title Text ( max: 31 chars )
#   2. Info Text To Show below title ( max: 1023 chars )
#   3. PNG Icons, relative to <aroma-assets> ( max width & height: 70 * <dp> )
#   4. Text. ( can readed from file, with "readfile_aroma" )
#
textbox(
  # Arg 1
    "Changelogs",
  
  # Arg 2
  "AROMA ROM Changelogs",
  
  # Arg 3
    "icons/info",
  
  # Arg 4
    readfile_aroma("sample.txt")
);


##
#
# Show please wait Interface.
#
pleasewait("Creating Test Temp File...");


##
#
# Create Temp text file. fill be store at
# /tmp/aroma-data/"test.txt"
#
writetmpfile("test.txt","This is test txt...");


##
#
# Show Alert Dialog...
#
# Arguments:
#   1. Title Text ( max: 31 chars )
#   2. Info Text To Show below title ( max: 512 chars )
#   3. PNG Icons, relative to <aroma-assets> ( max width & height: 70 * <dp> )
#   4. OK Button Text. if not defined, it will use "OK"
#
alert(
  # Arg 1
    "Temp File Has Been Created",
  
  # Arg 2
    "Test with: \n"+
    " > adb shell\n"+
    " # cd /tmp/aroma-data\n"+
    " # cat test.txt\n\n"+
    "Thanks...",
  
  # Arg 3
    "icons/info",
  
  # Arg 4
    "Yahoo..."
);



pleasewait("Test Variable & Calculation...");
##
#
# Test Variable and Math Calculation
#
  setvar("foo","Initial Value");
  appendvar("foo"," Append After");
  prependvar("foo","Prepend Before ");

#
# result: "Prepend Before Initial Value Append After"
#
  setvar("calculating",
    # 40 * (5+10)
    cal("40","*",cal("5","+","10"))
  );


pleasewait("Get Partitions Size...");
#
# getdisksize( path to mount point, unit size - m=MB, k=KB, b=byte )
#
setvar("systemsize",getdisksize("/system","k"));
setvar("sdextsize", getdisksize("/sd-ext","m"));


#
# Show Data In Alert
#
alert(
  # Arg 1
    "Test Variables & Calc",
  
  # Arg 2
    "foo: \n"+
    getvar("foo")+"\n"+
    
    "calculating: \n"+
      getvar("calculating")+"\n\n"+
    
    "/system: "+
    getvar("systemsize")+"KB\n"+
    
    "/sd-ext: \n"+
    getvar("sdextsize")+"MB\n"+
  
  # Arg 3
    "icons/alert",
  
  # Arg 4
    "Yeah"
);

##
# Show Menubox
#
menubox(
  # Title
    "Install Type",
  
  # Sub Title
    "Lorem Ipsum Dolore",
  
  # Icon
    "icons/install",
    
  # Will be saved in /tmp/aroma-data/type.prop
    "type.prop",
    
  # Items ( per 3 arguments ): Title, Subtitle/description, Icon
    # Item 1
    "Typical",    "Install default recommended packages into your phone.", "icons/install",
    
    # Item 2
    "Customize",  "Install only selected packages into your phone.", "icons/apps"
);

##
# Select Box
#
selectbox(
  # Title
    "Select Main Mods",
  
  # Sub Title
    "Please Select ROM Mods Below:",
  
  # Icon
    "icons/apps",
  
  # Will be saved in /tmp/aroma-data/mods.prop
    "mods.prop",
  
  # Items ( per 3 arguments ): Title, Subtitle/description, Item Type
  #   Item Type:
  #   0 = Unchecked by default
  #   1 = Checked by default
  #   2 = Item Group Title ( the subtitle won't be used )
  #
    # Group 1
    "SD-EXT Mods","",2,
      
      # Item 1.1
      "Generic APP2SD",
        "Default Mods with APKs in sd-ext, and Data in Internal Storage", 1,
      
      # Item 1.2
      "AD2SDX by amarullz",
        "Enhanced Mods with All non-system files in sd-ext",0,
    
    # Group 2
    "Boot Animation","",2,
      # Item 2.1
      "Galaxy Nexus",
        "Boot Animation from Galaxy Nexus ROM.", 1,
      
      # Item 2.2
      "Generic HTC",
        "Generic White HTC Quietly Briliant Boot Animation.",0
);

##
# Check Box
#
checkbox(
  # Title
    "Select Main Mods",
  
  # Sub Title
    "Please Select Apps To Install:",
  
  # Icon
    "icons/personalize",
  
  # Will be saved in /tmp/aroma-data/mods.prop
    "mods.prop",
  
  # Items ( per 3 arguments ): Title, Subtitle/description, Item Type
  #   Item Type:
  #   0 = Unchecked by default
  #   1 = Checked by default
  #   2 = Item Group Title ( the subtitle won't be used )
  #
    # Group 1
    "SD-EXT Mods","",2,
      
      # Item 1.1
      "Generic APP2SD",
        "Default Mods with APKs in sd-ext, and Data in Internal Storage", 1,
      
      # Item 1.2
      "AD2SDX by amarullz",
        "Enhanced Mods with All non-system files in sd-ext",1,
    
    # Group 2
    "Boot Animation","",2,
      # Item 2.1
      "Galaxy Nexus",
        "Boot Animation from Galaxy Nexus ROM.", 1,
      
      # Item 2.2
      "Generic HTC",
        "Generic White HTC Quietly Briliant Boot Animation.",1
);

#
# WONT DO ANYTHING IN TEST BUILD
#
setvar("retstatus",
  install(
    "Installing",
    "Installing AROMA INSTALLER...\nPlease Wait Until it Finished...",
    "icons/install",
    "Install Sucessfull Install Sucessfull Install Sucessfull Install Sucessfull Install Sucessfull Install Sucessfull Install Sucessfull Install Sucessfull Install Sucessfull Install Sucessfull Install Sucessfull Install Sucessfull Install Sucessfull Install Sucessfull Install Sucessfull Install Sucessfull Install Sucessfull Install Sucessfull"
  )
);

# Set Next Text fo Finish
ini_set("text_next", "Finish");

viewbox(
  "Installation Completed",
    "<#080>Congratulation...</#>\n\n"+
    "AROMA ROM Already Installed Into Your Phone\n"+
    "Press Finish, and Reboot your phone.\n\nThanks\n\n"+
    "Installer Status: "+getvar("retstatus"),
  "icons/info"
);

if
  confirm(
    # Arg 1
      "Reboot",
    
    # Arg 2
      "Reboot Now",
    
    # Arg 3
      "icons/info",
    
    # Arg 4 (Yes)
      "Reboot",
      
    # Arg 5 (No)
      "No"
  )=="yes"
then
  #
  # reboot("onfinish");   - Reboot if anything finished
  # reboot("now");        - Reboot Directly
  # reboot("disable");    - If you set reboot("now") before, use this command to revert it.
  #
  reboot("onfinish");
endif;